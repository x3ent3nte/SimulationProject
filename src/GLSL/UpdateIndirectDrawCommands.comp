#version 460
#extension GL_ARB_separate_shader_objects : enable

#define X_DIM 512

layout (local_size_x = X_DIM, local_size_y = 1, local_size_z = 1) in;

struct AgentRenderInfo {
    uint typeId;
    vec3 position;
    vec4 rotation;
};

struct VkDrawIndexedIndirectCommand {
    uint indexCount;
    uint instanceCount;
    uint firstIndex;
    int vertexOffset;
    uint firstInstance;
};

readonly layout (std430, binding = 0) buffer agentsBuffer {
    AgentRenderInfo agents[];
};

writeonly layout (std430, binding = 1) buffer drawCommandsBuffer {
    VkDrawIndexedIndirectCommand drawCommands[];
};

readonly layout (std430, binding = 2) buffer numberOfElementsBuffer {
    uint numberOfElements;
};

void main() {
    const uint index = gl_GlobalInvocationID.x;

    if (index >= numberOfElements) { return; }

    const AgentRenderInfo agent = agents[index];

    if ((index == 0) || (agents[index - 1].typeId != agent.typeId)) {
        drawCommands[agent.typeId].firstInstance = index;
    }

    if ((index == (numberOfElements - 1)) || (agent.typeId != agents[index + 1].typeId)) {
        drawCommands[agent.typeId].instanceCount = index + 1;
    }
}
