#version 460
#extension GL_ARB_separate_shader_objects : enable

#define X_DIM 512
#define GRAVITATIONAL_CONSTANT 6.67408e-11

layout (local_size_x = X_DIM, local_size_y = 1, local_size_z = 1) in;

struct Agent {
    uint typeId;
    int playerId;
    vec3 position;
    vec3 velocity;
    vec3 acceleration;
    vec3 target;
    vec3 rotationalVelocity;
    vec4 rotation;
    float radius;
    float mass;
};

struct MassiveAgent {
    vec3 position;
    float mass;
};

layout (std430, binding = 0) buffer agentsBuffer {
    Agent agents[];
};

readonly layout (std430, binding = 1) buffer massiveAgentsBuffer {
    MassiveAgent massiveAgents[];
};

readonly layout (std430, binding = 2) buffer scanBuffer {
    int scanned[];
};

readonly layout (std430, binding = 3) buffer timeDeltaBuffer {
    float timeDelta;
};

readonly layout (std430, binding = 4) buffer numberOfAgentsBuffer {
    uint numberOfAgents;
};

void main() {
    const uint index = gl_GlobalInvocationID.x;

    if (index >= numberOfAgents) { return; }

    Agent agent = agents[index];

    const int numberOfMassiveAgents = scanned[numberOfAgents - 1];
    for (int i = 0; i < numberOfMassiveAgents; ++i) {
        MassiveAgent massiveAgent = massiveAgents[i];

        const vec3 delta = massiveAgent.position - agent.position;
        const float dist = length(delta);
        if (dist > 0.0f) {
            const float force = (massiveAgent.mass * agent.mass) / (dist * dist);
            const vec3 deltaNorm = delta / dist;
            const vec3 forceVec = deltaNorm * force;
            const vec3 acceleration = forceVec / agent.mass;
            agent.velocity += acceleration * timeDelta;
        }
    }

    agents[index] = agent;
}
