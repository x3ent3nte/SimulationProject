#version 460
#extension GL_ARB_separate_shader_objects : enable

#define X_DIM 512

#define DETECTION_RADIUS 45

#define PI 3.14159265358979323846264338327950288

layout (local_size_x = X_DIM, local_size_y = 1, local_size_z = 1) in;

struct Agent {
    int playerId;
    vec3 position;
    vec3 velocity;
    vec3 acceleration;
    vec3 target;
    vec4 rotation;
    vec4 rotationalVelocity;
    float radius;
};

layout (std430, binding = 0) buffer bufAgentsIn {
    Agent agentsIn[];
};

layout (std430, binding = 1) buffer bufAgentsOut {
    Agent agentsOut[];
};

layout (std430, binding = 2) buffer bufReproduction {
    uint reproduction[];
};

layout (std430, binding = 3) buffer bufferTimeDelta {
    float timeDelta;
};

layout (std430, binding = 4) buffer bufferNumberOfElements {
    uint numberOfElements;
};

layout (std430, binding = 5) buffer bufferPlayerInputStates {
    uint playerInputStates[];
};

struct Steering {
    vec3 alignment;
    vec3 cohesion;
    vec3 separation;
};

vec3 safeNormalize(vec3 v) {
    float mag = length(v);
    if (mag <= 0.0f) {
        return vec3(1.0f, 0.0f, 0.0f);
    } else {
        return v / mag;
    }
}

float cosineSimilarity(vec3 a, vec3 b) {
    float magMult = length(a) * length(b);
    if (magMult <= 0.0f) {
        return 0.0f;
    }

    return dot(a, b) / magMult;
}

const vec3 canonicalForward = vec3(0.0f, 0.0f, -1.0f);
const vec3 canonicalUp = vec3(0.0f, 1.0f, 0.0f);
const vec3 canonicalRight = vec3(1.0f, 0.0f, 0.0f);

vec3 axisAndThetaToEuler(vec3 axis, float theta) {
    float forwardSim = cosineSimilarity(axis, canonicalForward);
    float upSim = cosineSimilarity(axis, canonicalUp);
    float rightSim = cosineSimilarity(axis, canonicalRight);

    return safeNormalize(vec3(rightSim, upSim, forwardSim)) * theta;
}

vec4 quaternionToAxisAndTheta(vec4 q) {
    float thetaHalved = acos(q.w);
    float sinThetaHalved = sin(thetaHalved);
    vec3 axis = vec3(0.0f, 0.0f, 0.0f);
    if (sinThetaHalved != 0.0f) {
        axis = q.xyz / sinThetaHalved;
    }
    return vec4(axis, thetaHalved * 2.0f);
}

vec4 hamiltonProduct(vec4 a, vec4 b) {
    float r = (a.w * b.w) - (a.x * b.x) - (a.y * b.y) - (a.z * b.z);
    float i = (a.w * b.x) + (a.x * b.w) + (a.y * b.z) - (a.z * b.y);
    float j = (a.w * b.y) - (a.x * b.z) + (a.y * b.w) + (a.z * b.x);
    float k = (a.w * b.z) + (a.x * b.y) - (a.y * b.x) + (a.z * b.w);

    return vec4(i, j, k, r);
}

vec4 inverseQuaternion(vec4 q) {
    return vec4(-q.x, -q.y, -q.z, q.w);
}

vec4 quaternionBetween(vec4 pre, vec4 post) {
    return hamiltonProduct(post, inverseQuaternion(pre));
}

vec3 rotatePointByQuaternion(vec3 p, vec4 q) {
    vec4 p4 = vec4(p, 0.0f);
    vec4 qi = inverseQuaternion(q);

    return hamiltonProduct(hamiltonProduct(q, p4), qi).xyz;
}

vec4 axisAndThetaToQuaternion(vec3 axis, float theta) {
    float thetaHalved = theta / 2;
    return vec4(axis * sin(thetaHalved), cos(thetaHalved));
}

vec4 eulerToQuaternion(vec4 euler) {
    vec4 q = axisAndThetaToQuaternion(canonicalRight, euler.x);
    q = normalize(hamiltonProduct(axisAndThetaToQuaternion(canonicalUp, euler.y), q));
    return normalize(hamiltonProduct(axisAndThetaToQuaternion(canonicalForward, euler.z), q));
}

vec3 calculateForward(vec4 rotation) {
    return rotatePointByQuaternion(vec3(0.0f, 0.0f, -1.0f), rotation);
}

void checkAgent(
    inout Agent agent,
    inout Agent otherAgent,
    inout vec3 sumAlignment,
    inout vec3 sumCohesion,
    inout vec3 sumSeparation,
    inout uint numBoids) {

    float distanceBetween = length(otherAgent.position - agent.position);
    if (distanceBetween < DETECTION_RADIUS) {
        vec3 otherAgentForward = calculateForward(otherAgent.rotation);

        //sumAlignment += otherAgentForward * (distanceBetween / DETECTION_RADIUS);
        sumAlignment += otherAgentForward;

        float tooCloseRadius = DETECTION_RADIUS / 2;

        if (distanceBetween < tooCloseRadius) {
            sumSeparation += (agent.position - otherAgent.position) * ((tooCloseRadius - distanceBetween) / tooCloseRadius);
        } else {
            //sumCohesion += (otherAgent.position - agent.position) * ((DETECTION_RADIUS - distanceBetween) / DETECTION_RADIUS);
            sumCohesion += (otherAgent.position - agent.position);
        }

        numBoids += 1;
    }
}

Steering calculateSteering(Agent agent, uint index) {

    vec3 sumAlignment = calculateForward(agent.rotation);
    vec3 sumCohesion = agent.position;
    vec3 sumSeparation = agent.position;
    uint numBoids = 1;

    for (uint i = index; i < numberOfElements - 1; ++i) {
        Agent otherAgent = agentsIn[i + 1];
        if (abs(otherAgent.position.x - agent.position.x) >= DETECTION_RADIUS) {
            break;
        }

        checkAgent(agent, otherAgent, sumAlignment, sumCohesion, sumSeparation, numBoids);
    }

    for (uint i = index; i >= 1; --i) {
        Agent otherAgent = agentsIn[i - 1];
        if (abs(otherAgent.position.x - agent.position.x) >= DETECTION_RADIUS) {
            break;
        }

        checkAgent(agent, otherAgent, sumAlignment, sumCohesion, sumSeparation, numBoids);
    }

    return Steering(
        safeNormalize(sumAlignment / numBoids),
        safeNormalize(sumCohesion / numBoids),
        safeNormalize(sumSeparation / numBoids));
}

vec3 calculateChangeInRotationalVelocity(vec3 current, vec3 desired) {
    float cosSim = cosineSimilarity(current, desired);
    float angleBetween = acos(cosSim);

    float theta = min(angleBetween, timeDelta * 30.0f);
    vec3 targetRight = cross(current, desired);
    //return axisAndThetaToQuaternion(targetRight, theta);
    return axisAndThetaToEuler(targetRight, theta);
}

const int kForwardBit = 0;
const int kBackBit = 1;
const int kLeftBit = 2;
const int kRightBit = 3;
const int kUpBit = 4;
const int kDownBit = 5;
const int kPitchUpBit = 6;
const int kPitchDownBit = 7;
const int kYawLeftBit = 8;
const int kYawRightBit = 9;
const int kRollLeftBit = 10;
const int kRollRightBit = 11;
const int kStabalizeRotationalVelocityBit = 12;
const int kStabalizeRotationBit = 13;
const int kSpawnBit = 14;

int computeInputValue(uint inputState, uint bitPos) {
    uint bitMask = 1 << bitPos;
    uint anded = inputState & bitMask;
    return int(anded >> bitPos);
}

void processPlayerAgent(inout Agent agent) {
    const uint inputState = playerInputStates[agent.playerId];

    const vec3 forward = rotatePointByQuaternion(canonicalForward, agent.rotation);
    const vec3 up = rotatePointByQuaternion(canonicalUp, agent.rotation);
    const vec3 right = cross(forward, up);

    const float accFloat = timeDelta * 2000.0f;
    const float rotationalAccFloat = timeDelta * 25.0f;

    vec3 acceleration = forward * accFloat * (computeInputValue(inputState, kForwardBit) - computeInputValue(inputState, kBackBit));
    acceleration += up * accFloat * (computeInputValue(inputState, kUpBit) - computeInputValue(inputState, kDownBit));
    acceleration += right * accFloat * (computeInputValue(inputState, kRightBit) - computeInputValue(inputState, kLeftBit));

    const int pitchInput = computeInputValue(inputState, kPitchUpBit) - computeInputValue(inputState, kPitchDownBit);
    const int yawInput = computeInputValue(inputState, kYawLeftBit) - computeInputValue(inputState, kYawRightBit);
    const int rollInput = computeInputValue(inputState, kRollRightBit) - computeInputValue(inputState, kRollLeftBit);

    /*vec4 rotationalAcceleration = axisAndThetaToQuaternion(right, rotationalAccFloat * pitchInput);
    rotationalAcceleration = normalize(hamiltonProduct(axisAndThetaToQuaternion(up, rotationalAccFloat * yawInput), rotationalAcceleration));
    rotationalAcceleration = normalize(hamiltonProduct(axisAndThetaToQuaternion(forward, rotationalAccFloat * rollInput), rotationalAcceleration));
    agent.rotationalVelocity = normalize(hamiltonProduct(rotationalAcceleration, agent.rotationalVelocity));*/

    vec3 euler = axisAndThetaToEuler(right, rotationalAccFloat * pitchInput);
    euler += axisAndThetaToEuler(up, rotationalAccFloat * yawInput);
    euler += axisAndThetaToEuler(forward, rotationalAccFloat * rollInput);

    if (computeInputValue(inputState, kStabalizeRotationalVelocityBit) == 1) {
        float rotationalVelocityMag = length(agent.rotationalVelocity.xyz);
        if (rotationalVelocityMag > 0) {
            float stabalizeAcc = min(rotationalAccFloat, rotationalVelocityMag);
            euler += -stabalizeAcc * (agent.rotationalVelocity.xyz / rotationalVelocityMag);
        }
    }

    if (computeInputValue(inputState, kStabalizeRotationBit) == 1) {
        vec4 rotationEuler = quaternionToAxisAndTheta(agent.rotation);
        vec3 diffEuler = vec3(0.0f, 0.0f, 0.0f) - rotationEuler.xyz;

        vec4 deltaQuaternion = quaternionBetween(agent.rotation, axisAndThetaToQuaternion(vec3(0.0f, 0.0f, 0.0f), 0.0f));
        vec4 deltaAxisAndTheta = quaternionToAxisAndTheta(deltaQuaternion);

        if (deltaAxisAndTheta.w > PI) {
            deltaAxisAndTheta.w = PI - deltaAxisAndTheta.w;
        }

        vec3 deltaEuler = axisAndThetaToEuler(deltaAxisAndTheta.xyz, deltaAxisAndTheta.w);
        float deltaEulerMag = length(deltaEuler);

        float direction = 1;
        if (deltaEulerMag > 0) {

            vec3 deltaEulerNorm = deltaEuler / deltaEulerMag;

            /*
            float eulerMag = length(agent.rotationalVelocity.xyz);
            if (eulerMag > 0) {
                vec3 eulerNorm = agent.rotationalVelocity.xyz / eulerMag;
                float cosSim = cosineSimilarity(eulerNorm, deltaEulerNorm);

                float currentThetaPerSecond = eulerMag * cosSim;

                float timeToTarget = deltaAxisAndTheta.w / rotationalAccFloat;
                float timeToDecelerate = currentThetaPerSecond / rotationalAccFloat;

                if (timeToTarget < timeToDecelerate) {
                    //direction = -1;
                }
            }
            */

            float stabalizeAcc = min(rotationalAccFloat, deltaEulerMag);
            euler += stabalizeAcc * deltaEulerNorm * direction;
        }

    }

    agent.rotationalVelocity.xyz += euler;

    agent.velocity += acceleration;
}

void main() {
    uint index = gl_GlobalInvocationID.x;

    if (index >= numberOfElements) {
        return;
    }

    Agent agent = agentsIn[index];

    vec3 forward = rotatePointByQuaternion(canonicalForward, agent.rotation);

    uint reproductionFactor = 1;

    if (agent.playerId < 0) {
        Steering steering = calculateSteering(agent, index);

        vec3 steeringVector = safeNormalize(
            (2 * steering.alignment) +
            (10 * steering.cohesion) +
            (4 * steering.separation));

        /*vec4 steeringQuaternion = calculateChangeInRotationalVelocity(forward, steeringVector);
        agent.rotationalVelocity = normalize(hamiltonProduct(steeringQuaternion, agent.rotationalVelocity));*/

        agent.rotationalVelocity.xyz += calculateChangeInRotationalVelocity(forward, steeringVector);

        agent.velocity = forward * 40.0f;
    } else {
        processPlayerAgent(agent);
        if (computeInputValue(playerInputStates[agent.playerId], kSpawnBit) == 1) {
            reproductionFactor = 2;
        }
    }

    agentsOut[index] = agent;

    reproduction[index] = reproductionFactor;
}
